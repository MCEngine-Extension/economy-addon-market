plugins {
    id 'com.gradleup.shadow' version '9.2.2'
    id 'java'
}

version = project.properties[project.name + '.version']
group = 'io.github.mcengine'

ext {
    // User credentials for GitHub Packages
    user_git_name = System.getenv('USER_GIT_NAME')
    user_github_token = System.getenv('USER_GITHUB_TOKEN')

    // Project JAR base name
    jarName = System.getenv('PROJECT_NAME') ?: 'MCEngineMarket'
}

allprojects {
    group = 'io.github.mcengine'

    apply plugin: 'java'
    apply plugin: 'com.gradleup.shadow'

    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }

        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://oss.sonatype.org/content/repositories/central' }

        ext.configureGitHubRepo = { repoName, repoUrl ->
            maven {
                name = repoName
                url = uri(repoUrl)
                credentials {
                    username = project.user_git_name
                    password = project.user_github_token ?: project.repository_token ?: project.organization_token
                }
            }
        }

        configureGitHubRepo('MCEngine Core API', 'https://maven.pkg.github.com/MCEngine-API/core')
        configureGitHubRepo('MCEngine Economy API', 'https://maven.pkg.github.com/MCEngine-API/economy')
        configureGitHubRepo('MCEngine Economy Common', 'https://maven.pkg.github.com/MCEngine-Common/economy')
    }
}

dependencies {
    // Core API
    compileOnly 'io.github.mcengine:core-api:2025.1.1-5'

    // Economy API + Common (migrated)
    compileOnly 'io.github.mcengine:economy-api:2025.1.1-5'
    compileOnly 'io.github.mcengine:economy-common:2025.1.1-5'

    // Minecraft API
    compileOnly 'org.spigotmc:spigot-api:1.21.10-R0.1-SNAPSHOT'
}

shadowJar {
    archiveClassifier = null
    archiveBaseName = project.jarName
}

tasks.named('jar').configure {
    enabled = false
}

build.dependsOn(shadowJar)
